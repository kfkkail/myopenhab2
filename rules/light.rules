var Timer sunsetTimer
var Timer dawnTimer
var Timer preDawnTimer

rule "Turn Off Lights at Midnight for the Night"
  when
    Time cron "0 0 0 ? * * *"
  then
    logDebug("rules", "Midnight - Turn OFF Lights at Midnight")

    sendCommand(OutdoorModule, OFF)
    sendCommand(OutdoorModule2, OFF)
    sendCommand(GarageDoorShutter, DOWN)
end


rule "Turn On/Off Lights at Dusk/Dawn"
  when
    Time cron "0 0 2 ? * * *" or
    Item TriggerTimer changed to ON
  then
    var DateTime daystart = new DateTime((Sunrise_Time.state as DateTimeType).calendar.timeInMillis)
    var DateTime dayend = new DateTime((Sunset_Time.state as DateTimeType).calendar.timeInMillis)
    logDebug("rules","Sunrise_Time: "+Sunrise_Time.state)
    logDebug("rules","Sunset_Time: "+Sunset_Time.state)
    logDebug("rules","Day Starts: "+daystart)
    logDebug("rules","Day Ends: "+dayend)

    // Cancel Timers to avoid reschedule
    if(sunsetTimer!=null) {
      logDebug("rules", "Timer sunsetTimer Cancelled")
      sunsetTimer.cancel()
    }
    if(preDawnTimer!=null) {
      logDebug("rules", "Timer preDawnTimer Cancelled")
      preDawnTimer.cancel()
    }
    if(dawnTimer!=null) {
      logDebug("rules", "Timer dawnTimer Cancelled")
      dawnTimer.cancel()
    }

    logDebug("rules","Create sunsetTimer")
    sunsetTimer = createTimer(dayend.minusMinutes(15)) [|
      logDebug("rules","sunsetTimer ended - Do some stuff at Sunset")
      sendCommand(GreatRoomPIRLightControl, ON)
      sendCommand(TalkControl, OFF)
      sendCommand(OutdoorModule, ON)
      sendCommand(OutdoorModule2, ON)
      ]

    logDebug("rules","Create preDawnTimer")
    preDawnTimer = createTimer(daystart.minusMinutes(60)) [|
      logDebug("rules","preDawnTimer ended - Turn On Christmas Lights")
      sendCommand(OutdoorModule, ON)
      sendCommand(OutdoorModule2, ON)
      ]

    logDebug("rules","Create dawnTimer")
    dawnTimer = createTimer(daystart.plusMinutes(15)) [|
      logDebug("rules","dawnTimer ended - Do some stuff at Sunrise")
      sendCommand(GreatRoomPIRLightControl, OFF)
      if (GreatRoomSwitch.state == OFF) {
        sendCommand(Kitchen_UnderCabinet1_Dimmer, 0)
        sendCommand(Kitchen_UnderCabinet2_Dimmer, 0)
      }
      sendCommand(OutdoorModule, OFF)
      sendCommand(OutdoorModule2, OFF)
      ]

    logDebug("rules","Sunset Timer: "+sunsetTimer)
    logDebug("rules","Predawn Timer: "+preDawnTimer)
    logDebug("rules","Dawn Timer: "+dawnTimer)
end